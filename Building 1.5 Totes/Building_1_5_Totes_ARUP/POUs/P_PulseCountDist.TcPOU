<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="P_PulseCountDist" Id="{27057743-4b0a-4dfc-b889-e1d9fe7f14cb}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM P_PulseCountDist
VAR
	EncoderValue : UDINT;
	StartValue: UDINT;
	EndValue: UDINT;
	Reading: BOOL;
	TotalCounts: UDINT;
	ReadCount: UDINT;
	ReadSum: UDINT;
	ReadAvg: REAL;
	Reset: BOOL;
	Timer: TON;
	TimeElapsed: REAL;
	Distance : REAL;
	Speed_ips : REAL;
	Speed_fps : REAL;
	Speed_fpm : REAL;
	DistperCount :	REAL;
	TestStart: R_TRIG;
	TestEnd: R_TRIG;
END_VAR

VAR CONSTANT
	EncoderMax: UDINT := Sorter.SORTERENCODERMAX_P2150;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Uncomment to get pulse counts for a distance
TestStart(CLK:= (* Start photo eye goes here --> *) Photoeyes.PE_11291.Out);
TestEnd(CLK:= (* End photo eye goes here --> *) Photoeyes.PE_10302.Out);
EncoderValue := GVL_IO.ENC_9031_Raw;

Timer(IN:= Reading, PT:= T#1H, Q=> , ET=> );

IF Reset THEN
	ReadCount := 0;	
	ReadSum := 0;
	ReadAvg := 0;
	Reset := FALSE;
END_IF

IF TestStart.Q THEN 
	StartValue := EncoderValue;
	Reading := TRUE;
	ReadCount := ReadCount +1;
END_IF

IF Reading THEN
	TimeElapsed := (TIME_TO_REAL(Timer.ET))/1000;
END_IF

IF TestEnd.Q THEN 
	EndValue := EncoderValue;
	IF EndValue < StartValue THEN
		TotalCounts := EncoderMax - StartValue;
		TotalCounts := TotalCounts + EndValue;
	ELSE
  		TotalCounts := (EndValue-StartValue);
	END_IF
	
	ReadSum := ReadSum + TotalCounts;
	IF  ReadCount > 0 THEN 
		ReadAvg := UDINT_TO_REAL(ReadSum)/UDINT_TO_REAL(ReadCount);
	END_IF
	 
	IF TimeElapsed > 0 THEN
		Speed_ips := (Distance/TimeElapsed);
		Speed_fps := Speed_ips/12;
		Speed_fpm := Speed_fps*60;
	END_IF
	IF TotalCounts > 0 THEN
		DistperCount := Distance / UDINT_TO_REAL(TotalCounts);
	END_IF
		Reading := FALSE;
END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>